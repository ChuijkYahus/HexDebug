plugins {
    alias(libs.plugins.shadow)
    alias(libs.plugins.modPublish)
}

archivesBaseName = getArtifactId("Forge")

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    accessWidenerPath = project(":Common").loom.accessWidenerPath

    forge {
        convertAccessWideners = true
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name

        mixinConfig "hexdebug-common.mixins.json"
        mixinConfig "hexdebug.mixins.json"
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin since it *excludes* files.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    modCompileOnly libs.findbugs.jsr305

    modImplementation libs.lsp4j
    modImplementation libs.lsp4j.debug

    forge libs.forge
    modApi libs.architectury.forge

    modImplementation(libs.hexcasting.forge) {
        transitive = false
    }

    // // Hex Casting Dependencies
    modCompileOnly libs.paucal.forge
    modCompileOnly libs.patchouli.forge

    modRuntimeOnly libs.paucal.forge
    modRuntimeOnly libs.patchouli.forge

    runtimeOnly libs.kotlin.forge

    modApi libs.clothConfig.forge

    common(project(path: ":Common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":Common", configuration: "transformProductionForge")) { transitive = false }
}

processResources {
    from(rootProject.file("Common/src/main/resources")) {
        include("data/*/patchouli_books/")
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set null
}

jar {
    archiveClassifier.set "dev"
}

sourcesJar {
    def commonSources = project(":Common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = project.archivesBaseName + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}

publishMods {
    file = remapJar.archiveFile
    additionalFiles.from(sourcesJar.archiveFile)
    changelog = getLatestChangelog()
    type = STABLE
    modLoaders.add("forge")

    version = project.version + "-forge"
    displayName = "HexDebug $project.version [Forge]"

    // Uncomment your desired platform(s)
//    curseforge {
//        accessToken = project.curseforgeApiToken
//        projectId = project.curseforgeID
//        minecraftVersions.add(minecraftVersion)
//
//        requires {
//            slug = "architectury-api"
//        }
//        requires {
//            slug = "kotlin-for-forge"
//        }
//        requires {
//            slug = "hexcasting"
//        }
//    }
//
//    modrinth {
//        accessToken = project.modrinthApiToken
//        projectId = project.modrinthID
//        minecraftVersions.add("1.19.2")
//
//        requires {
//            slug = "architectury-api"
//        }
//        requires {
//            slug = "kotlin-for-forge"
//        }
//        requires {
//            slug = "hex-casting"
//        }
//    }
}
